# this is a snakemake pipeline for post review steps after manual curation with JBT

# author: Yutang Chen,  MPB, ETH Zurich

# 15.07.2023

################################################################################
# # copy the reviewed assembly file to the working folder
# cp ~/public/Yutangchen/Sikem_hifi/juice_grass/sikem.dip.review.assembly ./

# wrap the original fasta to facilitate post review script
# with the wrapped fasta, the post review script runs much faster
# awk -f /scratch/yutang/juicer/3d-dna/utils/wrap-fasta-sequence.awk references/sikem_dip.fasta > references/sikem_dip_wrapped.fasta

# post review
# /scratch/yutang/juicer/3d-dna/run-asm-pipeline-post-review.sh -g 100 -r sikem.dip.review.assembly references/sikem_dip_wrapped.fasta aligned/merged_nodups.txt 2>&1 | tee post_review_assembly.log

# generate FINAL.cprops file
# after post review, the headers of the origianl fasta were lost
# I found the solution to rename the headers with the following one liner
# cat <(seqkit range -r 1:{params.chr} {input.fasta} | seqkit replace -p HiC_scaffold_ -r 'chr') <(seqkit range -r {params.unplaced}:-1 {input.fasta} | seqkit replace -p HiC_scaffold_ -r 'unplaced') > {output}
# the order of chromosomes did not change after post review. 
# For example, in the FINAL fasta after post review, scaffold 1, 2 ,3... corresponds to chr1, 2, 3... or whatever names specified before
################################################################################
# user configuration
ASM = 'fp170016.h1.fasta'
ASM_wrapped = 'fp170016_h1_wrapped.fasta'
reviewed_assembly = 'fp170016.review.assembly'
mnd_file = 'merged_nodups.txt'

# post review output
FINAL_fasta = 'fp170016_h1_wrapped.FINAL.fasta'
FINAL_fasta_renamed = 'fp170016_h1_wrapped.FINAL.renamed.fasta'

# number of chromosomes
chromosomes = 7

# path to the fasta wrapper script
wrap_fasta_script = '/scratch/yutang/juicer/3d-dna/utils/wrap-fasta-sequence.awk'

# path to post review script
post_review_script = '/scratch/yutang/juicer/3d-dna/run-asm-pipeline-post-review.sh'

# path to generate cprops script
generate_cprops_script = '/scratch/yutang/juicer/3d-dna/utils/generate-cprops-file.awk'

################################################################################

rule all:
	input:
		FINAL_fasta_renamed

################################################################################
# first wrap the original fasta
rule wrap_original_fasta:
	input:
		'references/' + ASM 
	output:
		'references/' + ASM_wrapped
	params:
		wrap_fasta = wrap_fasta_script
	shell:
		'''
		awk -f {params.wrap_fasta} {input} > {output}
		'''

# now do the post review script
rule post_review_pipeline:
	input:
		assembly = reviewed_assembly,
		wrapped_fasta = 'references/' + ASM_wrapped,
		mnd = 'aligned/' + mnd_file
	output:
		fasta = FINAL_fasta,
		success = 'post_review.success'
	params:
		post_review = post_review_script
	shell:
		'''
		{params.post_review} -g 100 -r {input.assembly} {input.wrapped_fasta} {input.mnd} && touch post_review.success
		'''

# now rename the FINAL assembly
rule rename_FINAL_assembly:
	input:
		fasta = FINAL_fasta,
		success = 'post_review.success'
	output:
		FINAL_fasta_renamed
	params:
		chr = chromosomes,
		unplaced = chromosomes + 1
	shell:
		'''
		cat <(seqkit range -r 1:{params.chr} {input.fasta} | seqkit replace -p HiC_scaffold_ -r 'chr') <(seqkit range -r {params.unplaced}:-1 {input.fasta} | seqkit replace -p HiC_scaffold_ -r 'unplaced') > {output}
		'''
